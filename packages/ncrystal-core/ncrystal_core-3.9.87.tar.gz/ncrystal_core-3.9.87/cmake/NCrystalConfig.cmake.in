################################################################################
##                                                                            ##
##  This file is part of NCrystal (see https://mctools.github.io/ncrystal/)   ##
##                                                                            ##
##  Copyright 2015-2024 NCrystal developers                                   ##
##                                                                            ##
##  Licensed under the Apache License, Version 2.0 (the "License");           ##
##  you may not use this file except in compliance with the License.          ##
##  You may obtain a copy of the License at                                   ##
##                                                                            ##
##      http://www.apache.org/licenses/LICENSE-2.0                            ##
##                                                                            ##
##  Unless required by applicable law or agreed to in writing, software       ##
##  distributed under the License is distributed on an "AS IS" BASIS,         ##
##  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.  ##
##  See the License for the specific language governing permissions and       ##
##  limitations under the License.                                            ##
##                                                                            ##
################################################################################

################################################################################
#                                                                              #
# Exports NCrystal targets, and provides a few paths and values of build       #
# options.                                                                     #
#                                                                              #
# Geant4 targets will not be exported unless the GEANT4BINDINGS component is   #
# explicitly requested                                                         #
#                                                                              #
################################################################################

#First make sure our file work when CMake is new enough, but the user have a
#very old version in their cmake_minimum_required statements (cf. github
#discussion/issue #137):
cmake_policy(PUSH)#NB: We POP at the end of this file.
cmake_policy(VERSION 3.16...3.30)

#Export a few directory paths (relocatable):
set( NCrystal_CMAKEDIR "${CMAKE_CURRENT_LIST_DIR}" )
get_filename_component( NCrystal_PREFIX "${NCrystal_CMAKEDIR}/@NCrystal_relpath_CMAKEDIR2ROOT@" ABSOLUTE )
get_filename_component( NCrystal_DATAROOT "${NCrystal_PREFIX}/@NCrystal_DATAROOT@" ABSOLUTE )

get_filename_component( NCrystal_BINDIR "${NCrystal_CMAKEDIR}/@NCrystal_relpath_CMAKEDIR2BINDIR@" ABSOLUTE )
get_filename_component( NCrystal_LIBDIR "${NCrystal_CMAKEDIR}/@NCrystal_relpath_CMAKEDIR2LIBDIR@" ABSOLUTE )
get_filename_component( NCrystal_INCDIR "${NCrystal_CMAKEDIR}/@NCrystal_relpath_CMAKEDIR2INCDIR@" ABSOLUTE )
if ( "x@NCRYSTAL_ENABLE_DATA@" STREQUAL "xON" )
  get_filename_component( NCrystal_DATAFILESDIR "${NCrystal_CMAKEDIR}/@NCrystal_relpath_CMAKEDIR2DATAFILESDIR@" ABSOLUTE )
  set( NCrystal_OPTION_INSTALL_DATA        "ON" )
else()
  set( NCrystal_DATAFILESDIR "")
  set( NCrystal_OPTION_INSTALL_DATA        "OFF" )
endif()

if ( "x@NCRYSTAL_ENABLE_DATA@" STREQUAL "xEMBED" )
  set( NCrystal_OPTION_EMBED_DATA          "ON" )
else()
  set( NCrystal_OPTION_EMBED_DATA          "OFF" )
endif()

#Config variables (backwards compatible names):
set( NCrystal_OPTION_BUILD_EXAMPLES      "@NCRYSTAL_ENABLE_EXAMPLES@" )
set( NCrystal_OPTION_BUILD_G4HOOKS       "@NCRYSTAL_GEANT4@" )
set( NCrystal_OPTION_INSTALL_PY          "OFF" )
set( NCrystal_OPTION_MODIFY_RPATH        "@NCRYSTAL_MODIFY_RPATH@" )
set( NCrystal_OPTION_NO_DIRECT_PYMODINST "OFF" )
set( NCrystal_OPTION_NAMESPACE "@NCRYSTAL_NAMESPACE@" )

#Config variables (autogenerated list):
set( _tmpnc_opts "@_NCrystal_all_opts@" )
set( _tmpnc_optsvals "@_NCrystal_all_opts_vals@" )
while( _tmpnc_opts )
  list( POP_FRONT _tmpnc_opts _tmpnc_o )
  list( POP_FRONT _tmpnc_optsvals _tmpnc_v )
  string( REPLACE "NCRYSTAL_" "NCrystal_OPT_" _tmpnc_o "${_tmpnc_o}" )
  set( "${_tmpnc_o}" "${_tmpnc_v}" )
endwhile()
unset( _tmpnc_opts )
unset( _tmpnc_optsvals )
unset( _tmpnc_o )
unset( _tmpnc_v )

#Libname + old school NCrystal_LIBRARIES variable:
set( NCrystal_LIBNAME "$<TARGET_FILE_NAME:NCrystal>" )#NB: generator expression
set( NCrystal_LIBRARIES "${NCrystal_LIBDIR}/${NCrystal_LIBNAME}" )

#Windows/scikitbuild mode needs fixup at the end of
#NCrystalTargets-<lowercaseconfig>.cmake:
if ( "@ncrystal_skbuild_shlib_in_wheel_scripts@" )
  function( _ncrystal_fixup_ncrystaltargets )
    execute_process(
      COMMAND ncrystal-config --show libpath
      OUTPUT_VARIABLE "_tmp_ncrystal_libpath" OUTPUT_STRIP_TRAILING_WHITESPACE
    )
    #Relying a bit too much on internal CMake implementation details for
    #comfort, but so far this is the only solution found:
    set(
      "_cmake_import_check_files_for_NCrystal::NCrystal"
      "${_tmp_ncrystal_libpath}"
      PARENT_SCOPE
    )
    get_filename_component( "tmp" "${CMAKE_CURRENT_LIST_FILE}" "NAME_WE")
    string( REPLACE "-" ";" "tmp" "${tmp}" )
    list(GET tmp -1 "tmp" )
    string( TOUPPER "${tmp}" "tmp" )

    set_target_properties(
      "NCrystal::NCrystal" PROPERTIES
      "IMPORTED_LOCATION_${tmp}"
      "${_tmp_ncrystal_libpath}"
    )
  endfunction()
endif()

#The NCrystal targets (not including the G4NCrystal targets!):
if(NOT TARGET NCrystal::NCrystal)
  include( "${NCrystal_CMAKEDIR}/NCrystalTargets.cmake" )
endif()

#For now GEANT4BINDINGS is the only optional component. To avoid injecting a
#dependency on Geant4 into non-Geant4 projects, the Geant4 dependency and G4NCrystal
#targets will only be added if the GEANT4BINDINGS component is explicitly
#requested.

set( NCrystal_COMPONENT_GEANT4BINDINGS "OFF" )

if ( NCrystal_OPTION_BUILD_G4HOOKS )
  #Build with Geant4 bindings. However, only load these targets (and the Geant4
  #dependency) if the GEANT4BINDINGS component was requested.
  if ( NCrystal_FIND_COMPONENTS )#if statements guards against CMP0085-OLD:
    if ( "GEANT4BINDINGS" IN_LIST NCrystal_FIND_COMPONENTS )
      if( NOT TARGET NCrystal::G4NCrystal )
        include( CMakeFindDependencyMacro )
        find_dependency( Geant4 "@Geant4_VERSION@" EXACT REQUIRED )
        include( "${NCrystal_CMAKEDIR}/G4NCrystalTargets.cmake" )
      endif()
      set( NCrystal_COMPONENT_GEANT4BINDINGS "ON" )
      #A few variables for old-school downstream cmake projects:
      set( G4NCrystal_LIBNAME "@G4NCrystal_LIBNAME@" )
      set( G4NCrystal_LIBDIR "${NCrystal_LIBDIR}" )
      set( G4NCrystal_INCDIR "${NCrystal_INCDIR}" )
      set( G4NCrystal_LIBRARIES "${NCrystal_LIBRARIES}" "${G4NCrystal_LIBDIR}/${G4NCrystal_LIBNAME}" )
    endif()
  endif()
else()
  if ( NCrystal_FIND_REQUIRED_GEANT4BINDINGS )
    #GEANT4BINDINGS were explicitly requested but this installation does not
    #support it.
    message("Skipping ineligible NCrystal installation due to absence of required GEANT4BINDINGS")
    set( NCrystal_FOUND "FALSE" )
  endif()
endif()

#Handle other requested components. For forward compatibility, silently ignore
#any requested component name we do not recognise, unless it is REQUIRED:
foreach(tmp ${NCrystal_FIND_COMPONENTS})
  if ( NCrystal_FIND_REQUIRED_${tmp} AND NOT "x${tmp}" STREQUAL "xGEANT4BINDINGS" )
    set( NCrystal_FOUND "FALSE" )
  endif()
endforeach()

#Undo the policy changes we did above:
cmake_policy(POP)
